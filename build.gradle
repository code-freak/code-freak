plugins {
  id 'org.jetbrains.kotlin.plugin.jpa' version '1.3.72'
  id 'org.springframework.boot' version '2.3.3.RELEASE'
  id 'org.jetbrains.kotlin.jvm' version '1.3.72'
  id 'org.jetbrains.kotlin.plugin.spring' version '1.3.72'
  id 'com.diffplug.spotless' version '5.1.1'
  id 'com.bertramlabs.asset-pipeline' version '3.0.10'
  id 'jacoco'
  id 'ua.eshepelyuk.ManifestClasspath' version '1.0.0'
  id "org.jetbrains.kotlin.kapt" version "1.3.72"
  id "com.gorylenko.gradle-git-properties" version "2.2.3"
  id "org.sonarqube" version "3.0"
  id 'net.researchgate.release' version '2.8.1'
  id 'org.liquibase.gradle' version '2.0.4'
  id 'com.google.cloud.tools.jib' version '2.5.0'
}

apply plugin: 'io.spring.dependency-management'

apply from: 'https://raw.githubusercontent.com/erikhofer/autodiff/1.0.2/autodiff.gradle'
project.autodiff.basePackage = "org.codefreak.codefreak.entity"
project.autodiff.changeLogMaster = "src/main/resources/db/changelog-master.yaml"
project.autodiff.changeLogDiff = project.hasProperty('changeLogDiff')
  ? project.property('changeLogDiff') : "src/main/resources/db/changelog-diff.yaml"

group = 'org.codefreak'
sourceCompatibility = '1.8'

repositories {
  mavenCentral()
  jcenter()
  maven { url 'https://jitpack.io' }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-data-ldap'
  implementation 'org.springframework.boot:spring-boot-starter-mail'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-batch'
  implementation 'org.springframework.security:spring-security-ldap'
  implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.2'
  implementation 'org.jetbrains.kotlin:kotlin-reflect'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation 'com.spotify:docker-client:8.16.0'
  implementation 'org.apache.commons:commons-compress:1.20'
  implementation 'org.apache.commons:commons-csv:1.8'
  implementation 'io.sentry:sentry-logback:1.7.30'
  implementation 'com.github.hsingh:java-shortuuid:2a4d72f'
  implementation 'com.github.Open-MBEE:junit-xml-parser:1.0.0'
  implementation 'org.mitre:openid-connect-client:1.3.3'
  implementation 'org.eclipse.jgit:org.eclipse.jgit:5.4.2.201908231537-r'
  implementation 'org.eclipse.jgit:org.eclipse.jgit.archive:5.8.1.202007141445-r'
  implementation 'org.liquibase:liquibase-core:4.0.0'
  implementation 'com.graphql-java-kickstart:graphql-spring-boot-starter:5.11.1'
  implementation 'com.expediagroup:graphql-kotlin-spring-server:3.6.1'
  implementation 'com.vladmihalcea:hibernate-types-52:2.9.13'
  implementation 'com.github.codefreak:templates:1.2.0'
  implementation 'com.networknt:json-schema-validator:1.0.43'
  implementation 'javax.validation:validation-api:2.0.1.Final'
  implementation 'org.hibernate:hibernate-validator:6.1.5.Final'
  kapt 'org.springframework.boot:spring-boot-configuration-processor'
  runtimeOnly 'org.springframework.boot:spring-boot-devtools'
  runtimeOnly 'org.postgresql:postgresql'
  runtimeOnly 'org.hsqldb:hsqldb'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.batch:spring-batch-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.hamcrest:hamcrest:2.2'
  testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
  testImplementation 'com.tngtech.archunit:archunit-junit4:0.11.0'

  // workaround for https://github.com/spotify/docker-client/issues/1030
  implementation 'org.glassfish.jersey.inject:jersey-hk2:2.31'

  // workaround for https://github.com/mitreid-connect/OpenID-Connect-Java-Spring-Server/issues/1468
  implementation 'org.springframework.security.oauth:spring-security-oauth2:2.5.0.RELEASE'

  liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:4.0.0'
  liquibaseRuntime 'com.h2database:h2:1.4.200'
  liquibaseRuntime 'org.yaml:snakeyaml:1.26'
  liquibaseRuntime sourceSets.main.runtimeClasspath
  liquibaseRuntime sourceSets.main.output
}

compileKotlin {
  kotlinOptions {
    freeCompilerArgs = ['-Xjsr305=strict']
    jvmTarget = '1.8'
  }
}

compileTestKotlin {
  kotlinOptions {
    freeCompilerArgs = ['-Xjsr305=strict']
    jvmTarget = '1.8'
  }
}

bootJar {
  // you can still build a fat jar for testing but this is NOT used for building
  // the Docker production image with jib!
  dependsOn 'deployFrontend'
}

spotless {
  kotlin {
    ktlint().userData(['indent_size': '2'])
  }
}

jacoco {
  toolVersion = '0.8.4'
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

springBoot {
  mainClassName = 'org.codefreak.codefreak.CodeFreakApplicationKt'
}

test {
  testLogging {
    events "failed"
    exceptionFormat "full"
  }
}

task vagrantTest(type: Test) {
  systemProperty "codefreak.docker.host", "http://localhost:2375"
  testLogging {
    events "failed"
    exceptionFormat "full"
  }
}

// Tests reports are required or Sonar will show 0% coverage
tasks.sonarqube.dependsOn jacocoTestReport
sonarqube {
  properties {
    property "sonar.projectKey", "code-freak_code-freak"
    property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
  }
}

task generateGraphqlSchema(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = "org.codefreak.codefreak.graphql.SchemaPrinterKt"
  args 'build/generated/graphql/schema.graphqls'
}

task deployFrontend(type: Copy) {
  from 'client/build'
  into 'build/resources/main/static'
  dependsOn "client:bundle"
}

jib {
  to {
    image = "cfreak/codefreak"
  }
  container {
    mainClass = springBoot.mainClassName
    ports = ["8080"]
    // set image build date to current timestamp
    creationTime = "USE_CURRENT_TIMESTAMP"
    environment = [
      ENV: "prod",
      SPRING_PROFILES_ACTIVE: "prod",
      SENTRY_ENVIRONMENT: "prod",
      SENTRY_RELEASE: project.version.toString()
    ]
  }
}
tasks.jibDockerBuild.dependsOn deployFrontend
